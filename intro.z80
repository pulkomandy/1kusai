; 1Kusai
; 1K intro by PulkoMandy/Shinra
; Graphics by Hokusai (public domain)

; ---------------------------------
; Useful system vectors
; ---------------------------------

SYS_CLS  equ $BBDB

SYS_PEN  equ $BBDE
	; A = new pen
	; AF is corrupt, and all other registers are preserved

SYS_GETPEN equ $BBE1

SYS_PAPER equ $BBE4
SYS_PRINT equ $BBFC
	; A = char to print (at cursor)

SYS_MOVE   equ $BBC0
	; AF, BC, DE and HL are corrupt
SYS_GETC   equ $BBC6
SYS_SETORG equ $BBC9
SYS_LINE   equ $BBF6
	; DE = X
	; HL = Y

SYS_INK equ $BC32
	; A = PEN
	; BC = INK (x2 because flashing)
	; AF, BC, DE and HL are corrupt, and all others are preserved

SYS_FILL equ $BD52
	; A = pen number
	; HL = work buffer
	; DE = work buffer size

; -----------------------------------------------------------------------------

SYS_REQ equ $BD61
	; (HL) = (DE)

SYS_I2REAL equ $BD64
	; HL = int to convert (in range 0..65535)
	; A = bit 7 set if HL is negative
	; DE = ptr to output (5 bytes)
	; AF and DE are corrupt

SYS_REAL2I equ $BD6A
	; HL = ptr to value
	; out: HL = converted value

SYS_ADD equ $BD7C
	; (HL) += (DE)

SYS_MUL equ $BD85
	; (HL) *= (DE)

SYS_DIV equ $BD88
	; (HL) /= (DE)

; Lets wrap all this float math stuff into nice macros
	macro FMUL
		LD DE,\1
		CALL SYS_MUL
	endmacro

	macro FDIV
		LD DE,\1
		CALL SYS_DIV
	endmacro

	macro FADD
		LD DE,\1
		CALL SYS_ADD
	endmacro

	macro FLOAD
		LD HL,\1
		LD DE,\2
		call SYS_REQ
	endmacro

; stuff...

    macro   print_hexa_16
        LD  A,H
        print_hexa_h
        LD  A,H
        print_hexa_l
        LD  A,L
        print_hexa_h
        LD  A,L
        print_hexa_l
    endmacro

    macro   print_hexa
    LD  \1,A
    print_hexa_h
    LD  A,\1
    print_hexa_l
    endmacro

    macro   print_hexa_h
    RRA
    RRA
    RRA
    RRA
    print_hexa_l
    endmacro

    macro print_hexa_l
    AND 0x0F
    ADD A,0x90
    DAA
    ADC A,0x40
    DAA
    CALL    $BD31
    endmacro


_start
; Set the color palette
	XOR A
	LD BC,$0A0A ; CYAN
	CALL SYS_INK

	LD A,1
	LD BC,$0101 ; BLUE
	CALL SYS_INK

	LD A,2
	LD BC,$1A1A ; WHITE
	CALL SYS_INK

	LD A,3
	LD BC,$0F0F ; ORANGE
	CALL SYS_INK

; Read polygon
; 1) Color
	LD IX,DATA

nextpoly
	LD A,(IX+4)

	CALL SYS_PEN

; 2) Initial point (READ + MOVE)

	LD E,(IX+0)
	LD D,(IX+1)
	PUSH DE

	LD L,(IX+2)
	LD H,(IX+3)
	PUSH HL

	CALL SYS_MOVE

; Convert initial point to REAL
	POP HL
	LD DE,Ay
	CALL I2REAL

	POP HL
	LD DE,Ax
	CALL I2REAL

; Advance to next point
	LD BC,5
nextpoint
	ADD IX,BC

	LD L,(IX+0)
	LD H,(IX+1)
	LD DE,Wx
	CALL I2REAL

	LD L,(IX+2)
	LD H,(IX+3)
	LD DE,Wy
	CALL I2REAL

	LD L,(IX+4)
	LD H,(IX+5)
	LD DE,Xx
	CALL I2REAL

	LD L,(IX+6)
	LD H,(IX+7)
	LD DE,Xy
	CALL I2REAL

	LD L,(IX+8)
	LD H,(IX+9)
	LD DE,Yx
	CALL I2REAL

	LD L,(IX+10)
	LD H,(IX+11)
	PUSH IX
	LD DE,Yy
	CALL I2REAL

	LD HL,0
	PUSH HL

	; -----------------------------
	; LOOP TO DRAW A BEZIER SEGMENT
	; It is drawn using 255 reference points and drawing straight lines
	; in between.
tloop:
	POP HL
	INC L
	JP Z,endtloop
	PUSH HL

	LD DE,t
	CALL I2REAL

	; Let t be in the range 0..1

	; u = 255
	LD H,0
	LD L,255
	LD DE,ux
	CALL I2REAL

	; t /= u
	LD HL,t
	FDIV ux

	FLOAD ux,t

	FMUL Wx
	FADD Xx
	FMUL t
	FADD Yx
	FMUL t
	FADD Ax

	; R2INT
	;LD HL,u
	CALL REAL2I

	; Store X coord
	PUSH HL

	; Do the same for Y coord
	; -----------------------

	FLOAD uy,t

	FMUL Wy
	FADD Xy
	FMUL t
	FADD Yy
	FMUL t
	FADD Ay

	; R2INT
	;LD HL,u
	CALL REAL2I

	; Draw line to computed point
	POP DE ; DE holds the X coord
	CALL SYS_LINE

	JP tloop

endtloop:

	; After the loop is complete, ux and uy are the float coordinates of the
	; last traced point. These are also the departure point for the next poly,
	; so copy them to Ax/Ay
	FLOAD Ax,ux
	FLOAD Ay,uy

	; Load size of a point for "advance to the next point" of next iteration
	LD BC,12

	; Is the polygon complete?
	POP IX
	; Here, IX points to the start of the current segment
	; At IX+12 we have either:
	; - Fill coordinates + pen, or
	; - Start of next segment
	; The fill coordinates are marked by 0x4000 in the first coord.
	LD A,(IX+13)
	BIT 6,A
	JP Z, nextpoint

	AND $3F
	LD D,A

	LD E,(IX+12)
	;LD D,(IX+13)
	LD L,(IX+14)
	LD H,(IX+15)
	CALL SYS_MOVE

	CALL SYS_GETPEN
	LD HL,fillbuf
	LD DE,$2000
	CALL SYS_FILL

	LD BC,16
	ADD IX,BC

	; Are we done with the data yet?
	PUSH IX
	CCF
	LD BC,endofdata
	SBC IX,BC
	POP IX

	; If not, proceed to the next polygon
	JP NZ, nextpoly

finished:
	LD A,'X'
	CALL $BB5A

; 4) More b√©ziers (COMPUTE + LINE)
; 5) Fill
	DI
	JR $

; SUBROUTINES
; ------------------------------------------------------

I2REAL
	LD A,H ; Copy the bit in A
	PUSH AF
	AND $3F ; Remove the bit from H
	LD H,A
	POP AF
	JP SYS_I2REAL

REAL2I
	CALL SYS_REAL2I
	RET P

	; Let's do a "NEG HL" if it should be negative
	xor a
	sub l
	ld l,a
	sbc a,a
	sub h
	ld h,a

	RET
	

N	equ	0x8000
E	equ	0x4000

DATA
; BIG WAVE ---------------------------------
;	   1       316     118     -166    10      378     -90     -198    252
	DEFW  316,      118     ; origin
	DEFB 1 ; pen
	DEFW  166 | N,   10     ; Yx Yx
	DEFW  378,       90 | N ; Xx Xy
	DEFW  198 | N,  252     ; Wx Wy

;	   (1       330     290)    452     202     -384    -1020   -396    498
	DEFW 452, 202, N | 384, N | 1020, N | 396, 498
;	   (1       2       -30)    -502    268     162     -96     654     -24
	DEFW N | 502, 268, 162, N | 96, 654, N | 24

	DEFW E|200,200 ; Fill coords

; FRONT BOAT --------------------------------
;	   3       160     130     0       -2      0       0       -54     -24
	DEFW 160, 130
	DEFB 3
	DEFW 0,N|2,0,0,N|54,N|24
;	   3       106     104     -198    296     648     -750    -396    480
	DEFW N|198,296,648,N|750,N|396,480
	DEFW E|150,128 ; FILL

; MT FUJI -----------------------------------

;	   1       356     110     -20     2       24      6       48      30
	DEFW 356,110
	DEFB 1
	DEFW N|20,2,24,6,48,30
;	   1       408     148     2       38      -6      -66     48      -12
	DEFW 2,38,N|6,N|66,48,N|12
;	   1       452     108     -36     92      -60     -90     0       0
	DEFW N|36,92,N|60,N|90,0,0

	DEFW E|380,116

; DROP --------------------------------------

;	   1       470     112     -28     0       30      6       30      6
	DEFW 470,112
	DEFB 1
	DEFW N|28,0,30,6,30,6
;	   1       502     124     70      12      -84     -54     -18     30
	DEFW 70,12,N|84,N|54,N|18,30
;	   1       470     112     0       0       0       0       0       0
	DEFW E|480,116

; BOAT TOP ----------------------------------

;	   1       574     192     -2      -6      6       6       0       -6
	DEFW 574,192
	DEFB 1
	DEFW N|2,N|6,6,6,0,N|6
;	   1       570     182     4       4       -6      -6      0       6
	DEFW 4,4,N|6,N|6,0,6
;	   1       568     186     0       0       0       0       6       6
	DEFW 4, 14, N|12, N|42, 0, 24
;	   1       566     188     -6      -6      18      18      -12     -18
	DEFW N|6,N|6,18,18,N|12,N|18
;	   1       566     182     2       6       0       -12     0       6
	DEFW N|2,26,N|42,N|54,0,0
;	   1       524     154     14      82      54      -36     -18     -36
	DEFW 14,82,54,N|36,N|18,N|36
;	   1       574     164     8       14      -30     -42     24      54
	DEFW 8,14,N|30,N|42,24,54
;	   1       576     190     -6      2       12      -6      -6      12
	DEFW N|6,2,12,N|6,N|6,12
;	   1       576     198     -2      2       6       -6      -6      6
	DEFW N|2,2,6,N|6,N|6,6

	DEFW E|570,186

; RIGHT BOAT HULL --------------------------------

;	   3       356     122     88      -82     -246    276     396     -114
	DEFW 356,122
	DEFB 3
	DEFW 88,N|82,N|246,276,396,N|114
;	   3       594     202     68      56      -204    54      -72     -210
	DEFW 68,56,N|204,54,N|72,N|210
;	   3       386     102     0       -4      0       6       -30     18
	DEFW 0,N|4,5,6,N|30,18

	DEFW E|380,112

; PEOPLE IN RIGHT BOAT ----------------------------

;	   2       554     160     12      -4      -12     18      -6      -12
	DEFW 554,160
	DEFB 2
	DEFW 12,N|4,N|12,18,N|6,N|12
;	   2       548     162     -12     -2      12      -12     6       12
	DEFW N|12,N|2,12,N|12,6,12

	DEFW E|554,160

;	   2       534     142     8       -12     0       24      -12     -6
	DEFW 534, 142
	DEFB 2
	DEFW 8,N|12,0,24,N|12,N|6
;	   2       530     148     -2      6       -6      -18     12      6
	DEFW N|2,6,N|6,N|18,12,6
	DEFW E|534,142

; Bottom boat

;	   3       444     2       80      -182    54      342     -312    -78
	DEFW 444,2
	DEFB 3
	DEFW    80, N|182, 54,342,N|312, N| 78
;	   3       266     84      -124    -210    198     390     156     -240
	DEFW N|124, N|210,198,390,  156, N|240
;	   3       496     24      -4      2       6       0       -54     -24
	DEFW N|  4,     2,  6,  0,N| 54, N| 24
	DEFW E|444,8

; Bottom boat bodies
	DEFW 454,20
	DEFB 1
	DEFW N|10,46,12,N|60,72,24
	DEFW 220,92,N|252,N|204,N|42,102

	DEFW E|500,25

; THE WAVE

;	   2       238     398     -112    140     654     -558    -780    0
	DEFW 238,398
	DEFB 2
	DEFW N|112,140,654,N|558,N|780,0
;	   2       0       -20     68      -138    -108    246     216     -6
	DEFW 68, N|138, N|108, 246, 216, N|6
;	   2       176     82      -396    242     546     240     312     -438
	DEFW N|396,242,546,240,312,N|438
;	   2       638     126     280     410     -510    -294    -150    -168
	DEFW 280,410,N|510,N|294,N|150,N|168
;	   2       258     74      -112    196     216     -318    0       90
	DEFW N|112,196,216,N|318,0,90
;	   2       362     42      154     -22     -54     -36     -102    96
	DEFW 154,N|22, N|54, N|36, N|102, 96
;	   2       360     80      46      -74     -336    318     570     -72
	DEFW 46,N|74,N|336, 318,570,N|72
;	   2       640     252     -66     -122    36      666     -390    -612
	DEFW N|66,N|122,36,666,N|390,N|612
;	   2       220     184     356     268     -378    -24     -180    -222
	DEFW 356,268,N|378,N|24,N|180,N|222
;	   2       18      206     -172    6       324     48      60      -24
	DEFW N|172,6,324,48,60,N|24
;	   2       230     236     -1020   64      1872    -456    -744    414
	DEFW N|1020,64,1872,N|456,N|744,414
;	   2       338     258     -20     -14     48      48      -6      -48
	DEFW N|20,N|14,48,48,N|6,N|48
;	   2       360     244     -140    -224    -102    300     120     78
	DEFW N|140,N|224,N|102,300,120,78
;	   2       238     398     0       0       0       0       0       0
	DEFW E|230, 380

; People in lower boat
	DEFW 456, 20
	DEFB 2
	DEFW 6, N|12,0,24,N|12,N|6
	DEFW N|12,12,0,N|36,18,18

	DEFW E|456, 20

	DEFW  468,   22
	DEFB 2
	DEFW N|34, N|28,  78, 30, N|42,    6
	DEFW N| 2,    4, N|6,  0,    6, N|12

	DEFW E|468, 22

	DEFW 494,24
	DEFB 2
	DEFW 4, N|10,    6,   18, N|12,  0
	DEFW 2,   28, N|24, N|48,   24, 12

	DEFW E|494,24

;	*/
endofdata


; storage space (not included in binary)
Ax	defs 5
Yx	defs 5
Xx	defs 5
Wx	defs 5

Ay	defs 5
Yy	defs 5
Xy	defs 5
Wy	defs 5

t	defs 5
uy	defs 5
ux	defs 5
fillbuf
